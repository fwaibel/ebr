configure(subprojects.findAll {it.name.contains('-') && !it.name.contains('SpringSource-EBR-templates')}) {

    apply plugin: 'base'

    repositories {
        mavenCentral()
        maven { url "http://build.eclipse.org/rt/virgo/maven/bundles/release" }
        maven { url "http://repository.springsource.com/maven/bundles/external" }
    }

    configurations {
        bundlorRuntime

        sourceBundle
    }

    dependencies {
        bundlorRuntime('org.eclipse.virgo.bundlor:org.eclipse.virgo.bundlor.commandline:1.1.2.RELEASE')
        bundlorRuntime('org.eclipse.virgo.bundlor:org.eclipse.virgo.bundlor:1.1.2.RELEASE')
        bundlorRuntime('org.eclipse.virgo.bundlor:org.eclipse.virgo.bundlor.blint:1.1.2.RELEASE')
    }

    task createBuildDir() {
        doLast() {
            file(project.buildDir).mkdir()
        }
    }

    task bundlor(type: JavaExec, dependsOn: createBuildDir) {

        classpath = configurations.bundlorRuntime

        def artifactName = project.name.split('-')[0]
        def artifactVersion = project.name.split('-')[1]

        main = 'org.eclipse.virgo.bundlor.commandline.Bundlor'
        args '-D', "version=${artifactVersion}"
        args '-i', "${->configurations.sourceBundle[0]}" // lazy GString to resolve the configuration at runtime
        args '-m', "${artifactName}.mf"
        args '-o', new File(file(project.buildDir), "${project.name}.jar")
    }
}

task wrapper(type: Wrapper) { gradleVersion = '2.10' }
