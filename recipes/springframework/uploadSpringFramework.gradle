plugins {
  id "org.hidetake.ssh" version "2.4.0"
}

remotes {
    eclipseDotOrg {
        host = ECLIPSE_ORG_FTP_HOST
        user =  ECLIPSE_ORG_FTP_USER
        identity = file(System.properties['user.home'] + "/.ssh/${ECLIPSE_ORG_FTP_IDENTITY}")
        // TODO review why build.eclipse.org is rejected
        // Caused by: com.jcraft.jsch.JSchException: reject HostKey: build.eclipse.org
        knownHosts = allowAnyHosts
//        knownHosts = file(System.properties['user.home'] + "/.ssh/known_hosts")
    }
}

def version = '4.2.4.RELEASE'
def groupId = 'org.eclipse.virgo.mirrored'
def mirrorPath = ECLIPSE_ORG_FTP_MIRROR_PATH

task uploadSpringFramework << {
    [
        "aop",
        "aspects",
        "beans",
        "context",
        "context.support",
        "core",
        "expression",
        "jdbc",
        "jms",
        "messaging",
        "orm",
        "oxm",
        "test",
        "transaction",
        "web",
        "webmvc",
        "webmvc.portlet",
        "websocket"
    ].each { module ->
        println "Uploading ${module} to ${remotes.eclipseDotOrg.host}..."
        def modulePath = "${groupId}/org.springframework.${module}/${version}"
        ssh.run {
            session(remotes.eclipseDotOrg) {
                execute "mkdir -p ${mirrorPath}/${modulePath}"
                put from: "${buildDir}/${modulePath}/ivy-${version}.xml", into: "${mirrorPath}/${modulePath}"
                put from: "${buildDir}/${modulePath}/ivy-${version}.xml.sha1", into: "${mirrorPath}/${modulePath}"
                put from: "${buildDir}/${modulePath}/org.springframework.${module}-${version}.jar", into: "${mirrorPath}/${modulePath}"
                put from: "${buildDir}/${modulePath}/org.springframework.${module}-${version}.jar.sha1", into: "${mirrorPath}/${modulePath}"
            }
        }
        println "done."
    }
}
